(ns serverless-lumo.build
  (:require fs path archiver
            [clojure.string :as str]
            [cljs.reader :as reader]
            [lumo.build.api]
            [lumo.classpath :as classpath]
            [lumo.core]
            [lumo.io :as io]
            [serverless-lumo.index :as index]))

(def ^:const zip-comment "Generated by serverless-cljs-plugin on ")

(defn zip!
  "Create the zip in output-dir. Return a promise containing the path.

  The zip-opts map is:

     {:dirs #{\"dir1\" \"dir2\"}
      :files #{[\"path/to/file\" {:name \"file\"}]
               \"another/file\"}}

  Seqable :files entries are passed as parameters to the archiver's
  .file function, for details check:
    - https://archiverjs.com/docs/Archiver.html#file"
  [output-path zip-opts compiler-opts]

  (let [archiver      (archiver "zip" #js {:zlib {:level 9}
                                           :comment (str zip-comment (.toISOString (js/Date.)))})
        output-stream (.createWriteStream fs output-path)]
    (js/Promise.
     (fn [resolve-fn reject-fn]
       (.on output-stream "close" #(resolve-fn output-path))
       (.on archiver "warning" (fn [err]
                                 (if (= (.-code err) "ENOENT")
                                   (js/console.warn (.-message err))
                                   (reject-fn err))))
       (.on archiver "error" reject-fn)
       (.pipe archiver output-stream)
       (doseq [d (:dirs zip-opts)]
         (.directory archiver d))
       (doseq [f (:files zip-opts)]
         (if (string? f)
           (.file archiver f)
           (.apply (.-file archiver) archiver (clj->js (seq f)))))
       (.finalize archiver)))))

(defn compile!
  "Compile ClojureScript using the lumo api."
  [inputs compiler-opts]
  (js/console.info
   "Invoking the Lumo compiler w/ inputs"
   (.stringify js/JSON (clj->js inputs)))
  (run! classpath/add! inputs)
  (lumo.build.api/build
   (apply lumo.build.api/inputs inputs)
   compiler-opts))

(defn read-conf!
  "Read and return the configuration map."
  []
  (try
    (-> "serverless-lumo.edn" io/slurp reader/read-string)
    (catch js/Error e
      (when-not (= (.. e -code) "ENOENT")
        (throw e)))))

(def ^{:doc "The default build config."}
  default-config
  {:source-paths ["src"]
   :compiler     {:output-to     (.format path #js {:dir "out" :base "lambda.js"})
                  :output-dir    "out"
                  :source-map    false ;; lumo bug
                  :target        :nodejs
                  :optimizations :none}})

(defn- output-dir [compiler]
  (let [s (:output-dir compiler)]
    (str/replace s #"/+$" "")))

(defn dump-index!
  [path functions compiler]
  (-> (index/generate-index functions compiler)
      (index/write-index! path)))

(defn build!
  "Build a project."
  [opts cljs-lambda-opts]
  (let [compiler   (:compiler cljs-lambda-opts)
        output-dir (output-dir compiler)
        index      (dump-index! (.resolve path (:zip-path opts) "../index.js")
                                (:functions opts)
                                compiler)]
    (when (:index opts)
      (dump-index! (.resolve path output-dir "../index.js")
                   (:functions opts)
                   compiler))
    (compile! (:source-paths cljs-lambda-opts) compiler)
    (zip! (:zip-path opts)
          {:dirs  #{output-dir "node_modules"}
           :files #{[index {:name "index.js"}]}}
          compiler)))

(def cli-option-map
  {:z :zip-path
   :i :index
   :f :functions})

(defmulti  parse-option (fn [k v] k))
(defmethod parse-option :default   [k v] v)
(defmethod parse-option :index     [k v] (reader/read-string v))
(defmethod parse-option :functions [k v] (reader/read-string v))

(defn cli-options "Compute the option map"
  [argv]
  (into {}
    (for [[k v] (partition 2 argv)
          :let [k (keyword (str/replace k #"^-{1,2}" ""))
                k (cli-option-map k k)]]
      [k (parse-option k v)])))

(defn cmd-line-args []
  (if-let [args cljs.core/*command-line-args*] ;; for lumo > 1.7.0
    args
    (when-let [args lumo.core/*command-line-args*] ;; for lumo <= 1.7.0
      args)))

(defn- merge-maps [x y]
  (merge-with
   (fn [x y]
     (if (map? x)
       (merge-maps x y)
       y))
   x y))

(defn ^:export -main [& args]
  (let [opts (cli-options (cmd-line-args))]
    (build! opts (merge-maps default-config (read-conf!)))))

(set! *main-cli-fn* -main)
